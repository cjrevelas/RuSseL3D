!RuSseL3D - Copyright (C) 2021 C. J. Revelas, A. P. Sgouros, A. T. Lakkas
!
!See the LICENSE file in the root directory for license information.

subroutine solver_edwards(ds, ns, mumps_matrix_type, q, q_final, node_in_q0_face)
!----------------------------------------------------------------------------------------------------------!
use kcw,      only: A_m, F_m, rdiag1
use geometry, only: numnp, all_el
#ifdef USE_MPI
use mpistuff
#endif
!----------------------------------------------------------------------------------------------------------!
implicit none
!----------------------------------------------------------------------------------------------------------!
#ifdef USE_MPI
include "mpif.h"
#endif
!----------------------------------------------------------------------------------------------------------!
integer, intent(in) :: ns, mumps_matrix_type
integer             :: i, j, kk, time_step, get_sys_time, t_init, t_final

logical, intent(in), dimension(numnp) :: node_in_q0_face

real(8), intent(in), dimension(ns+1)          :: ds
real(8), intent(inout), dimension(2,numnp)    :: q
real(8), intent(inout), dimension(ns+1,numnp) :: q_final
!----------------------------------------------------------------------------------------------------------!
t_init = get_sys_time()

do time_step = 2, ns+1
    !set essential BCs and form the LHS of the linear system of equations to be solved
    call solver_dirichlet(ds(time_step), mumps_matrix_type, node_in_q0_face)

#ifdef USE_MPI
    !send a continue (.true.) signal to the slaves
    call MPI_BCAST(.true., 1, MPI_LOGICAL, 0, MPI_COMM_WORLD, ierr)
#endif

    if (ns.ge.10.and.MOD(time_step+1,ns/10).eq.0) then
        write(6,'(I3,1X)',advance='no') NINT((time_step-2.d0)/ns*100.d0)
    elseif (ns.lt.10.and.MOD(time_step+1,1).eq.0) then
        write(6,'(I3,1X)',advance='no') NINT((time_step-2.d0)/ns*100.d0)
    endif

    !form the RHS of the linear system of equations to be solved
    rdiag1 = 0.

    do kk = 1, all_el
        i = F_m%row(kk)
        j = F_m%col(kk)

        rdiag1(i) = rdiag1(i) + F_m%rh(kk)*q(1,j)
    enddo

    do i = 1, numnp
        if (node_in_q0_face(i)) rdiag1(i) = 0.
    enddo

    !solve the linear system of equations
    call solver_mumps(mumps_matrix_type)

    !update solution/propagator
    do kk = 1,numnp
         q(2,kk) = rdiag1(kk)
    enddo

    !save propagators for convolution
    do kk = 1,numnp
        q_final(time_step,kk) = q(2,kk)
        q(1,kk)               = q(2,kk)
    enddo

    !deallocate the A arrays generated by dirichlet subroutine
    deallocate(A_m%value)
    deallocate(A_m%col)
    deallocate(A_m%row)
enddo

write(6,'(1X,I3)',advance='no') 100

t_final = get_sys_time()

write(6,'(" :",I6)') t_final - t_init

return
!----------------------------------------------------------------------------------------------------------!
end subroutine solver_edwards
