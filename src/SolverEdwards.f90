!RuSseL3D - Copyright (C) 2021 C. J. Revelas, A. P. Sgouros, A. T. Lakkas
!
!See the LICENSE file in the root directory for license information.

subroutine SolverEdwards(stepSize, numSteps, qq, qqFinal, nodeBelongsToDirichletFace, elemcon)
!----------------------------------------------------------------------------------------------------------!
use kcw_mod,         only: A_m, F_m, rdiag1
use parser_vars_mod, only: numDirichletFaces, numNanopFaces,    &
                           dirichletFaceValue, nanopFaceValue,  &
                           dirichletFaceId, dirichletFaceValue, &
                           nanopFaceId, nanopFaceValue,         &
                           mumpsMatrixType
use geometry_mod,    only: numNodes, numTotalNodePairs, nodeBelongsToFaceId
use fhash_module__ints_double
#ifdef USE_MPI
use mpistuff_mod
#endif
!----------------------------------------------------------------------------------------------------------!
implicit none
!----------------------------------------------------------------------------------------------------------!
#ifdef USE_MPI
include "mpif.h"
#endif
!----------------------------------------------------------------------------------------------------------!
integer, intent(in) :: numSteps
integer             :: step, nodePair, node, row, col, ToolsSystemTime, tInit, tFinal, face

logical, intent(in), dimension(numNodes) :: nodeBelongsToDirichletFace

type(fhash_type__ints_double), intent(inout) :: elemcon

real(8), intent(in), dimension(numSteps+1)             :: stepSize
real(8), intent(inout), dimension(2,numNodes)          :: qq
real(8), intent(inout), dimension(numSteps+1,numNodes) :: qqFinal
!----------------------------------------------------------------------------------------------------------!
tInit = ToolsSystemTime()

do step = 2, numSteps+1
  CALL FemNonZeroEntries(stepSize(step), nodeBelongsToDirichletFace, elemcon)

#ifdef USE_MPI
  ! Send a continue (.True.) signal to the slaves
  CALL MPI_BCAST(.True., 1, MPI_LOGICAL, 0, MPI_COMM_WORLD, ierr)
#endif

  if (numSteps.ge.10.and.MOD(step+1,numSteps/10).eq.0) then
    write(6,'(I3,1X)',advance='no') NINT((step-2.0d0)/numSteps*100.0d0)
  elseif (numSteps.lt.10.and.MOD(step+1,1).eq.0) then
    write(6,'(I3,1X)',advance='no') NINT((step-2.0d0)/numSteps*100.0d0)
  endif

  ! Form the RHS of the linear system of equations to be solved
  rdiag1 = 0.0d0

  do nodePair = 1, numTotalNodePairs
    if (F_m%isZero(nodePair)) cycle
    if (F_m%row(nodePair)==0) cycle ! TEMP SOLUTION

    row = F_m%row(nodePair)
    col = F_m%col(nodePair)

    rdiag1(row) = rdiag1(row) + F_m%rh(nodePair) * qq(1,col)
  enddo

  ! Assign value at the Dirichlet boundaries
  do face = 1, numDirichletFaces
    do node = 1, numNodes
      if (nodeBelongsToFaceId(node) == dirichletFaceId(face)) rdiag1(node) = dirichletFaceValue(face)
    enddo
  enddo

  ! Assign value at the Nanoparticle boundaries
  do face = 1, numNanopFaces
    do node = 1, numNodes
      if (nodeBelongsToFaceId(node) == nanopFaceId(face)) rdiag1(node) = nanopFaceValue(face)
    enddo
  enddo

  ! Solve the linear system of equations
  CALL SolverMumps(mumpsMatrixType)

  ! Update solution/propagator
  do node = 1, numNodes
    qq(2,node) = rdiag1(node)
  enddo

  ! Save propagators for convolution
  do node = 1, numNodes
    qqFinal(step,node) = qq(2,node)
    qq(1,node)         = qq(2,node)
  enddo

  ! Deallocate the A arrays generated by dirichlet subroutine
  deallocate(A_m%value)
  deallocate(A_m%col)
  deallocate(A_m%row)
enddo

write(6,'(1X,I3)',advance='no') 100

tFinal = ToolsSystemTime()

write(6,'(" :",I6)') tFinal - tInit

return
!----------------------------------------------------------------------------------------------------------!
end subroutine SolverEdwards
