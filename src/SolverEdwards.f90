!RuSseL3D - Copyright (C) 2021 C. J. Revelas, A. P. Sgouros, A. T. Lakkas
!
!See the LICENSE file in the root directory for license information.

subroutine SolverEdwards(ds, ns, qq, qqFinal, nodeBelongsToDirichletFace, elemcon)
!----------------------------------------------------------------------------------------------------------!
use kcw_mod,         only: A_m, F_m, rdiag1
use parser_vars_mod, only: numDirichletFaces, numNanopFaces,    &
                           dirichletFaceValue, nanopFaceValue,  &
                           dirichletFaceId, dirichletFaceValue, &
                           nanopFaceId, nanopFaceValue,         &
                           mumpsMatrixType
use geometry_mod,    only: numNodes, numTotalNodePairs, nodeBelongsToFaceId
use fhash_module__ints_double
#ifdef USE_MPI
use mpistuff_mod
#endif
!----------------------------------------------------------------------------------------------------------!
implicit none
!----------------------------------------------------------------------------------------------------------!
#ifdef USE_MPI
include "mpif.h"
#endif
!----------------------------------------------------------------------------------------------------------!
integer, intent(in) :: ns
integer             :: ii, jj, kk, step, ToolsSystemTime, tInit, tFinal, face

logical, intent(in), dimension(numNodes) :: nodeBelongsToDirichletFace

type(fhash_type__ints_double), intent(inout) :: elemcon

real(8), intent(in), dimension(ns+1)             :: ds
real(8), intent(inout), dimension(2,numNodes)    :: qq
real(8), intent(inout), dimension(ns+1,numNodes) :: qqFinal
!----------------------------------------------------------------------------------------------------------!
tInit = ToolsSystemTime()

do step = 2, ns+1
  call FemNonZeroEntries(ds(step), nodeBelongsToDirichletFace, elemcon)

#ifdef USE_MPI
  ! Send a continue (.true.) signal to the slaves
  call MPI_BCAST(.True., 1, MPI_LOGICAL, 0, MPI_COMM_WORLD, ierr)
#endif

  if (ns.ge.10.and.MOD(step+1,ns/10).eq.0) then
    write(6,'(I3,1X)',advance='no') NINT((step-2.0d0)/ns*100.0d0)
  elseif (ns.lt.10.and.MOD(step+1,1).eq.0) then
    write(6,'(I3,1X)',advance='no') NINT((step-2.0d0)/ns*100.0d0)
  endif

  ! Form the RHS of the linear system of equations to be solved
  rdiag1 = 0.0d0

  do kk = 1, numTotalNodePairs
    if (F_m%is_zero(kk)) cycle
    if (F_m%row(kk)==0) cycle ! TEMP SOLUTION

    ii = F_m%row(kk)
    jj = F_m%col(kk)

    rdiag1(ii) = rdiag1(ii) + F_m%rh(kk) * qq(1,jj)
  enddo

  ! Assing value at the Dirichlet boundaries
  do face = 1, numDirichletFaces
    do ii = 1, numNodes
      if (nodeBelongsToFaceId(ii) == dirichletFaceId(face)) rdiag1(ii) = dirichletFaceValue(face)
    enddo
  enddo

  do face = 1, numNanopFaces
    do ii = 1, numNodes
      if (nodeBelongsToFaceId(ii) == nanopFaceId(face)) rdiag1(ii) = nanopFaceValue(face)
    enddo
  enddo

  ! Solve the linear system of equations
  call SolverMumps(mumpsMatrixType)

  ! Update solution/propagator
  do kk = 1,numNodes
    qq(2,kk) = rdiag1(kk)
  enddo

  ! Save propagators for convolution
  do kk = 1,numNodes
    qqFinal(step,kk) = qq(2,kk)
    qq(1,kk)         = qq(2,kk)
  enddo

  ! Deallocate the A arrays generated by dirichlet subroutine
  deallocate(A_m%value)
  deallocate(A_m%col)
  deallocate(A_m%row)
enddo

write(6,'(1X,I3)',advance='no') 100

tFinal = ToolsSystemTime()

write(6,'(" :",I6)') tFinal - tInit

return
!----------------------------------------------------------------------------------------------------------!
end subroutine SolverEdwards
